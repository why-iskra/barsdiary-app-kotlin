plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 31

    lintOptions {
        disable 'PrivateResource'
        checkReleaseBuilds false
    }

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        if (versionPropsFile.canRead()) {
            versionProps.load(new FileInputStream(versionPropsFile))
        } else {
            versionProps['VERSION_CODE']='0'
        }

        def isRelease = gradle.startParameter.taskRequests.any {
            it.args.any {it.endsWith("Release") }
        }

        def code = 0
        if(isRelease) {
            code = versionProps['VERSION_CODE'].toInteger() + 1
        } else {
            code = versionProps['VERSION_CODE'].toInteger()
        }

        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "ru.unit.barsdiary"
            minSdk 26
            targetSdk 31
            versionCode code
            versionName "2.6-beta ($code)"
        }
    } else {
        throw new GradleException("Could not read version.properties")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        beta { }
        debug { }
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation project(':sdk')
    implementation project(':data')
    implementation project(':domain')

    implementation platform("com.google.firebase:firebase-bom:$firebase_bom_version")

    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation "nl.dionsegijn:konfetti:$konfetti_version"
    implementation "com.facebook.shimmer:shimmer:$shimmer_version"
    implementation "com.squareup.picasso:picasso:$picasso_version"
    implementation "jp.wasabeef:picasso-transformations:$picasso_transformations_version"

    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraintlayout_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:$androidx_fragment_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$androidx_navigation_version"
    implementation "androidx.legacy:legacy-support-v4:$androidx_legacy_version"
    implementation "androidx.paging:paging-runtime-ktx:$androidx_paging_version"
    implementation "androidx.datastore:datastore-preferences:$androidx_datastore_version"
    implementation "androidx.viewpager2:viewpager2:$androidx_viewpager2_version"

    implementation "com.google.android.material:material:$material_version"
}